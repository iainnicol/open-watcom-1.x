#
# Get Module name from .def file.
# Read it from following line form.
#
#  LIBRARY "module_name"
#    or
#  LIBRARY module_name
#
fopen %1 1 r
assign %k = 0
assign %(ModuleName) = ""
assign %(status) = 0
loop
    if %(status) == 0
        assign %b = ""
        fread 1 %b
        if (lastrc != 0)
            assign %k = 1
        else
            assign %l = strlen %b
            assign %(status) = 1
        endif
    elseif %(status) == 1
        assign %a = substr %b 1 7
        if ("%a" != "LIBRARY")
            assign %(status) = 0
        else
            assign %b = substr %b 9 %l
            assign %l = strlen %b
            assign %n = strchr %b "
            if (%n > 0)
                expr %n = %n + 1
                assign %b = substr %b %n %l
                assign %l = strchr %b "
                expr %l = %l - 1
                assign %b = substr %b 1 %l
            endif
            assign %n = 0
            assign %(status) = 2
        endif
    elseif %(status) == 2
        expr %n = %n + 1
        assign %c = substr %b %n %n
        if ("%c" != " ") && ("%c" != "	") # there must be TAB char, don't change
            assign %(m1) = %n
            assign %(m2) = %n
            assign %(status) = 3
        elseif %n >= %l
            assign %k = 1
        endif
    elseif %(status) == 3
        expr %n = %n + 1
        assign %c = substr %b %n %n
        if ("%c" == " ") || ("%c" == "	") # there must be TAB char, don't change
            assign %(ModuleName) = substr %b %(m1) %(m2)
            assign %k = 1
        elseif %n >= %l
            assign %(ModuleName) = substr %b %(m1) %l
            assign %k = 1
        else
            assign %(m2) = %n
        endif
    endif
until (%k)
fclose 1
